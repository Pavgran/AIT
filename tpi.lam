-- for comments, see tpi.hs and tuple_numerals.lam
\io. let
  cons = \a\b\p. p a b;
  Csucc = \n\f\x. f (n f x);
  Cadd  = \a\b\f\x. a f (b f x);
  bit0 = \x\y. x;
  bit1 = \x\y. y;
  Tadd = \tm\tn\x. tm (tn x);
  id = \x. x;
  Tsub = \tm\tn\x. tm id (tn x);
  CTmul = \c\t. c t;

  go = \Tb\Ta\Tc. let
      prod = \bit\Ta'\Cn. cons bit (go (Tadd Tb Tb) (Tadd Ta' Ta') Tc Cn) in
      (Tc (\_. prod bit1 (Tsub Tc Ta)))      -- case Ta >= Tc
      (Ta (\_.                               -- case Ta < Tc
         (Tc
             (\_\Cn. let Cn1 = Csucc Cn;     -- case Ta+Tb >= Tc
                    x2np1 = CTmul (Cadd (Csucc Cn) Cn);
                    CnxTb = CTmul Cn Tb
              in go CnxTb (Tadd (x2np1 Ta) CnxTb) (x2np1 Tc) Cn1))
         (Ta (Tb (\_. prod bit0 Ta)))        -- case Ta+Tb < Tc
      ));

  C1 = id;
  T1 = \x\f. f x;
  tpi = go T1 T1 T1 C1
in
  tpi
