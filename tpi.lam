-- for comments, see tpi.hs and tuple_numerals.lam
\io. let
  cons = \a\b\p. p a b;
  Csucc = \n\f\x. f (n f x);
  Cadd  = \a\b\f\x. a f (b f x);
  bit0 = \x\y. x;
  bit1 = \x\y. y;
  Tadd = \tm\tn\x. tm (tn x);
  id = \x. x;
  Tsub = \tm\tn\x. tm id (tn x);
  CTmul = \c\t. c t;

  go = \Cn\Tb\Ta\Tc. let
      prod = \bit\Ta'. cons bit (go Cn (Tadd Tb Tb) (Tadd Ta' Ta') Tc) in
      (Tc (\_. prod bit1 (Tsub Tc Ta)))      -- case Ta >= Tc
      (Ta (\_.                               -- case Ta < Tc
         (Tc
             (\_. let Cn1 = Csucc Cn;        -- case Ta+Tb >= Tc
                    x2np1 = CTmul (Cadd Cn Cn1);
                    cnxTb = CTmul Cn Tb
              in go Cn1 cnxTb (Tadd (x2np1 Ta) cnxTb) (x2np1 Tc)))
         (Ta (Tb (\_. prod bit0 Ta)))        -- case Ta+Tb < Tc
      ));

  C1 = id;
  T1 = \x\f. f x;
  tpi = go C1 T1 T1 T1
in
  tpi
