-- See https://googology.fandom.com/wiki/Church-Kleene_ordinal for more
let
    -- Church numerals
    C0 = \f\x. x;
    Csucc = \n.\f\x. f (n f x);
    C1 = Csucc C0;
    C2 = Csucc C1;
    C16 = C2 C2 C2;
    C64K = C2 C2 C2 C2;
    -- Kleene ordinals, correspondong to data Ord = Zero | Succ Ord | Lim (Nat -> Ord)
    Zero    =    \z\s\l. z;      -- ordinal 0
    Succ    = \n.\z\s\l. s n;    -- successor ordinal n+1
    Lim     = \f.\z\s\l. l f;    -- limit ordinal sup { f C0, f C1, f C2, ... }
    -- The set K of ordinal notations is defined inductively by
    -- Zero is in K
    -- if n is in K, then Succ n is in K
    -- if f Cn is in K for all Church numerals Cn, then Lim f is in K
    w  = Lim (\n.n Succ Zero); -- limit ordinal sup { 0, 1, 2, ... }
    O0 = Zero;    -- size 10
    O1 = Succ O0; -- size 21
    O2 = Succ O1; -- size 40
    O3 = Succ O2; -- size 44
    Ow = w;       -- size 46
    O4 = Succ O3; -- size 48
    Ow1 = Succ Ow; -- size 60;
    -- Fundamental sequence for w1CK
    -- w1CKi is smallest (in BLC size) ordinal exceeding all w1CKj for j < i
    -- similar to a Busy Beaver for ordinals
    w1CK0 = O0;
    w1CK1 = O1;
    w1CK2 = O2;
    w1CK3 = O3;
    w1CK4 = Ow;  -- smaller than O4
    w1CK5 = Ow1; -- must be larger than w1CK4 
in Ow
