let BMS = let
  0 = \f\x. x;
  1 = \f. f;
  notZero = \n\a\b. n (\u.a) b;
  pred = \n\f\x. n (\g\h.h (g f))(\u.x)(\u.u);
  gt = \x\y. notZero (y pred x); -- greater than
  succ = \n\f\x. f (n f x);

  expand = \mod\m\i. let m1 = m 1 in
    gt i m1
      (m (m1 pred (succ i)))    --  copy array element i+1 - m1
      (let msis = m (succ i) succ;  in msis (
        notZero i
          (gt (msis i) (m1 succ mod) m1 0)
          (pred (m i))));        -- new array length = old length - 1 + m1

  matrix0 = \n\k. gt k n 0 n;    -- single column of n n's

  -- bms
  in \rows. let
    -- mod is in [0..rows-1]
    -- bms = \matrix\mod. matrix 0 (\_.succ (bms (expand mod matrix) (pred (mod (\_.mod) rows)))) mod
    -- cannot replace succ here since a 0 mod will wipe out the count

    -- bms1 mat (mod+1) == bms mod
    bms1 = \matrix\mod1. matrix 0 (\_. mod1 (let mod = pred mod1 in bms1 (expand mod matrix) (mod (\_.mod) rows))) mod1
    -- replacing succ by mod1 is ok, since a mod1=1 is more than compensated by exponentiation for other values

  -- in bms (matrix0 rows) (pred rows);
  in bms1 (matrix0 rows) rows;

  2 = \f\x. f (f x)
in 2 2 BMS 2                      -- 488 bits; 61 bytes

--						G | B0 | C

-- 2 2             = (0,0)(1,1)			| 0 | 1
--  2                (0,0)(2,2)			| 0 | 1

-- 2 0 2           = (0,0)(1,0)(2)		0 | 1 | 2
--  3                (0,0)(2,0)(2)		0 | 0 |

-- 0 4 0 2         = (0,0)(1,0)(1,0)		| 0 1 | 1
--  4                (0,0)(2,0)(4,0)		| 0 0 | 0

-- 4 0 2           = (0,0)(1,0)(1)		| 0 1 | 1
--  3                (0,0)(2,0)(4)		| 0 0 |

-- 0 2 0 0 0 2     = (0,0)(1,0)(0,0)(1,0)	0 1 | 0 | 1
--  6                (0,0)(2,0)(0,0)(2,0)	0 0 | 0 | 0

-- 2 0 0 0 2       = (0,0)(1,0)(0,0)(1)		0 1 | 0 | 1
--  5                (0,0)(2,0)(0,0)(2)		0 0 | 0 |

-- 0 0 0 0 0 2     = (0,0)(1,0)(0,0)(0,0)	0 1 0 | 0
--  6                (0,0)(2,0)(0,0)(0,0)	0 0 0 | 0

-- 0 0 0 0 2       = (0,0)(1,0)(0,0)(0)		0 1 0 | 0
--  5                (0,0)(2,0)(0,0)(0)		0 0 0 |

-- 0 0 0 2         = (0,0)(1,0)(0,0)		0 1 | 0
--  4                (0,0)(2,0)(0,0)		0 0 | 0

-- 0 0 2           = (0,0)(1,0)(0)		0 1 | 0
--  3                (0,0)(2,0)(0)		0 0 |

-- 0 2             = (0,0)(1,0)			0 | 1
--  2                (0,0)(2,0)			0 | 0

-- 2               = (0,0)(1)			| 0 | 1
--  1                (0,0)(2)			| 0 |

-- 0 0             = (0,0)(0,0)			| 0
--  2                (0,0)(0,0)			| 0

-- 0               = (0,0)(0)			| 0
--  1                (0,0)(0)			|

-- HALT            = (0,0)			|
--  0                (0,0)			|
