let BMS = let
  0 = \f\x. x;
  1 = \f. f;
  notZero = \n\a. n (\_.a);
  succ = \n\f\x. f (n f x);
  pred = \n\f\x. n (\g\h.h (g f))(\u.x)(\u.u);

  expand = \mod\m\i. let m1 = m 1 in
    m1 pred i                       -- rewrite from gt i m1
      (\_. m (m1 pred (succ i)))    --  copy array element i+1 - m1
      (let msis = m (succ i) succ;  in msis (
        notZero i
          ((m1 succ mod) pred (msis i) (\_.m1) 0)     -- rewrite from gt (msis i) (m1 succ mod)
          (pred (m i))));        -- new array length = old length - 1 + m1

  matrix0 = \n\k. n pred k (\_.0) n;    -- single column of n n's  -- rewrite from gt k n

  -- bms
  in \rows. let
    -- mod is in [0..rows-1]
    bms = \matrix\mod. matrix 0 (bms (expand mod matrix) (pred (mod (\_.mod) rows))) rows
    -- this returns rows^recurse^arraylength where recursion ends when arraylength = 0
  in bms (matrix0 rows) (pred rows);

  2 = \f\x. f (f x)
in 2 2 BMS 2                      -- 446 bits; 55.75 bytes

--						G | B0 | C

-- 2 2             = (0,0)(1,1)			| 0 | 1
--  2                (0,0)(2,2)			| 0 | 1

-- 2 0 2           = (0,0)(1,0)(2)		0 | 1 | 2
--  3                (0,0)(2,0)(2)		0 | 0 |

-- 0 4 0 2         = (0,0)(1,0)(1,0)		| 0 1 | 1
--  4                (0,0)(2,0)(4,0)		| 0 0 | 0

-- 4 0 2           = (0,0)(1,0)(1)		| 0 1 | 1
--  3                (0,0)(2,0)(4)		| 0 0 |

-- 0 2 0 0 0 2     = (0,0)(1,0)(0,0)(1,0)	0 1 | 0 | 1
--  6                (0,0)(2,0)(0,0)(2,0)	0 0 | 0 | 0

-- 2 0 0 0 2       = (0,0)(1,0)(0,0)(1)		0 1 | 0 | 1
--  5                (0,0)(2,0)(0,0)(2)		0 0 | 0 |

-- 0 0 0 0 0 2     = (0,0)(1,0)(0,0)(0,0)	0 1 0 | 0
--  6                (0,0)(2,0)(0,0)(0,0)	0 0 0 | 0

-- 0 0 0 0 2       = (0,0)(1,0)(0,0)(0)		0 1 0 | 0
--  5                (0,0)(2,0)(0,0)(0)		0 0 0 |

-- 0 0 0 2         = (0,0)(1,0)(0,0)		0 1 | 0
--  4                (0,0)(2,0)(0,0)		0 0 | 0

-- 0 0 2           = (0,0)(1,0)(0)		0 1 | 0
--  3                (0,0)(2,0)(0)		0 0 |

-- 0 2             = (0,0)(1,0)			0 | 1
--  2                (0,0)(2,0)			0 | 0

-- 2               = (0,0)(1)			| 0 | 1
--  1                (0,0)(2)			| 0 |

-- 0 0             = (0,0)(0,0)			| 0
--  2                (0,0)(0,0)			| 0

-- 0               = (0,0)(0)			| 0
--  1                (0,0)(0)			|

-- HALT            = (0,0)			|
--  0                (0,0)			|
